// GPU Instancer Pro
// Copyright (c) GurBu Technologies


#include_with_pragmas "Packages/com.gurbu.gpui-pro/Runtime/Compute/Include/PlatformDefines.hlsl"
#include "Packages/com.gurbu.gpui-pro/Runtime/Compute/Include/GPUIDefines.hlsl"
#include "Packages/com.gurbu.gpui-pro/Runtime/Compute/Include/Matrix.hlsl"
#include "../Include/GPUITerrainDefines.hlsl"

#pragma kernel CSTreeGenerationKernel

#pragma multi_compile _ GPUI_TREE_INSTANCE_COLOR

#ifdef __INTELLISENSE__
#define GPUI_TREE_INSTANCE_COLOR
#endif 

// Result buffers
RWStructuredBuffer<float4x4> gpuiTransformBuffer;
RWStructuredBuffer<GPUICounterData> counterBuffer;
#ifdef GPUI_TREE_INSTANCE_COLOR
RWStructuredBuffer<float4> gpuiTreeInstanceDataBuffer;
#endif

// Input buffers
StructuredBuffer<TerrainTreeInstance> treeData;

uniform uint bufferSize;
uniform uint transformBufferStartIndex;
uniform int prototypeIndex;
uniform float3 terrainSize;
uniform float3 terrainPosition;
uniform float3 prefabScale;
uniform bool applyPrefabScale;
uniform bool applyRotation;
uniform bool applyHeight;

inline float4 EncodeFloatRGBA(float v)
{
    float4 kEncodeMul = float4(1.0, 255.0, 65025.0, 16581375.0);
    float kEncodeBit = 1.0 / 255.0;
    float4 enc = kEncodeMul * v;
    enc = frac(enc);
    enc -= enc.yzww * kEncodeBit;
    enc.a = 1;
    return enc;
}

[numthreads(GPUI_THREADS, 1, 1)]
void CSTreeGenerationKernel(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= bufferSize)
        return;
    
    TerrainTreeInstance treeInstance = treeData[id.x];

    if (treeInstance.prototypeIndex != prototypeIndex)
        return;
    
    uint index;
    InterlockedAdd(counterBuffer[treeInstance.prototypeIndex].count, 1, index);
    
    float4x4 rotation = identityMatrix;
    if (applyRotation)
        rotation = MatrixRotate(vector3Up, treeInstance.rotation);
    
    float3 scale = vector3One;
    if (applyHeight)
        scale = float3(treeInstance.widthScale, treeInstance.heightScale, treeInstance.widthScale);
    if (applyPrefabScale)
        scale *= prefabScale;
    
    gpuiTransformBuffer[index + transformBufferStartIndex] = TRS(
    float3(
            treeInstance.position.x * terrainSize.x + terrainPosition.x,
            treeInstance.position.y * terrainSize.y + terrainPosition.y,
            treeInstance.position.z * terrainSize.z + terrainPosition.z
            ),
        rotation,
        scale
    );
    
#ifdef GPUI_TREE_INSTANCE_COLOR
    gpuiTreeInstanceDataBuffer[index] = EncodeFloatRGBA(treeInstance.color);
#endif
}